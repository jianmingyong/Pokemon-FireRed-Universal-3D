version=2
##############################
# Sub Global.Save
##############################

@Storage.set(str,StackTrace.Set,Version\2\Class\Global\Save.dat)
@Script.start(Version\2\Class\StackTrace\Set)

:if:<Storage.get(bool,Security.FileValidation.Checks)>=true
	@Storage.set(bool,Security.FileValidation.ScriptExist,true)
	:end
:endif

:if:<not><Register.value(Global.GameName)>=Nothing
	@Script.start(Version\2\Class\GameMode\<Register.value(Global.GameName)>)

	:if:<not><Level.filename>=GlobalMain
		# Save Player Position
		@Register.change(Global.Save.Player.Position.X.<Register.value(Global.GameName)>,<Player.position(x)>)
		@Register.change(Global.Save.Player.Position.Y.<Register.value(Global.GameName)>,<Player.position(y)>)
		@Register.change(Global.Save.Player.Position.Z.<Register.value(Global.GameName)>,<Player.position(z)>)
		@Register.change(Global.Save.Player.Facing.<Register.value(Global.GameName)>,<Player.facing>)
		@Register.change(Global.Save.Player.ThirdPerson.<Register.value(Global.GameName)>,<Player.thirdperson>)
		@Register.change(Global.Save.Player.Level.<Register.value(Global.GameName)>,<Level.file>)
		
		# Save Player Party
		@Storage.set(int,i,1)
		:while:<not><Storage.get(int,i)>><Pokemon.count>
			@Register.change(Global.Save.Pokemon.Party.<Storage.get(int,i)>.<Register.value(Global.GameName)>,<Pokemon.data(<Math.int(<Storage.get(int,i)>-1)>)>)
			@Storage.update(int,i,add,1)
		:endwhile
		:while:<Storage.get(int,i)><7
			@Register.change(Global.Save.Pokemon.Party.<Storage.get(int,i)>.<Register.value(Global.GameName)>,Nothing)
			@Storage.update(int,i,add,1)
		:endwhile
		@Pokemon.clear
		
		# Save Player Badge
		@Storage.set(int,i,1)
		:while:<not><Storage.get(int,i)>><Storage.get(int,GameMode.MaxBadges)>
			@Register.change(Global.Save.Player.Badge.<Storage.get(int,i)>.<Register.value(Global.GameName)>,<Player.hasbadge(<Storage.get(int,GameMode.Badge.ID.<Storage.get(int,i)>)>)>)
			@Player.removebadge(<Storage.get(int,GameMode.Badge.ID.<Storage.get(int,i)>)>)
			@Storage.update(int,i,add,1)
		:endwhile
		
		# Save Player Inventory
		@Storage.set(int,i,1)
		:while:<Storage.get(int,i)><9999
			:if:<Inventory.count(<Storage.get(int,i)>)>>0
				@Register.change(Global.Save.Item.<Storage.get(int,i)>.<Register.value(Global.GameName)>,<Inventory.count(<Storage.get(int,i)>)>)
			:endif
			:if:<Inventory.count(<Storage.get(int,i)>)>=0 <and> <Register.registered(Global.Save.Item.<Storage.get(int,i)>.<Register.value(Global.GameName)>)>=true
				@Register.unregister(Global.Save.Item.<Storage.get(int,i)>.<Register.value(Global.GameName)>,int)
			:endif
			@Storage.update(int,i,add,1)
		:endwhile
		@Item.clearitem
		
		# Save Game Flag
		@Register.change(Global.Load.<Register.value(Global.GameName)>,true)
	:endif
	
	@Player.save
:endif
:end